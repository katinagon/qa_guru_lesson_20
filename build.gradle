plugins {
    id 'java'
    id 'io.qameta.allure' version '2.12.0'
}

allure {
    report {
        version.set("2.21.0")
    }
    adapter { // отвечает за появление папочки build/allure-results
        aspectjWeaver.set(true) //обработка аннотации @Step
        frameworks {
            junit5 { //название фреймворка
                adapterVersion.set("2.21.0") //версия интеграции фреймворка и Allure
            }
        }
    }
}

repositories {
    mavenCentral()
}

def selenideVersion = "7.5.1",
    restAssuredVersion = "5.5.5",
    allureVersion = "2.21.0"

dependencies {
    testImplementation (
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            'org.junit.jupiter:junit-jupiter:5.9.3',
            'org.slf4j:slf4j-simple:2.0.7',
            'org.aeonbits.owner:owner:1.0.12',
            "io.appium:java-client:8.3.0",
    )
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    useJUnitPlatform()
    systemProperties(System.getProperties())

    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

tasks.register("platformTest", Test) {
    useJUnitPlatform()
    systemProperty 'platform', System.getProperty("platform", "android")
    systemProperty 'bs_user', System.getenv("BROWSERSTACK_USER")
    systemProperty 'bs_key', System.getenv("BROWSERSTACK_KEY")
}